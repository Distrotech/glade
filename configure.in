dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.52)
AC_INIT(glade-3, 2.91.1,
	http://bugzilla.gnome.org/enter_bug.cgi?product=glade3)
AC_CANONICAL_SYSTEM

AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)
AM_MAINTAINER_MODE
AM_CONFIG_HEADER(config.h)

AC_PROG_INTLTOOL([0.29])
AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)
AC_PATH_PROG(DLLTOOL, dlltool)

AC_ISC_POSIX
AC_PROG_CC
AC_DISABLE_STATIC
AC_PROG_INSTALL
AC_PROG_MAKE_SET

AC_LIBTOOL_DLOPEN
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL

# If the source code has changed at all, increment GLADE_REVISION
# If any interfaces have been added, removed, or changed, increment GLADE_CURRENT, and set GLADE_REVISION to 0.
# If any interfaces have been added since the last public release, then increment GLADE_AGE.
# If any interfaces have been removed since the last public release, then set GLADE_AGE to 0.
# Reference: http://www.gnu.org/software/libtool/manual.html#Versioning
GLADE_REVISION=1
GLADE_CURRENT=0
GLADE_AGE=0
AC_SUBST(GLADE_REVISION)
AC_SUBST(GLADE_CURRENT)
AC_SUBST(GLADE_AGE)


dnl ================================================================
dnl Gettext stuff.
dnl ================================================================
GETTEXT_PACKAGE=AC_PACKAGE_NAME
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Name of the gettext package.])
ALL_LINGUAS="az da en_CA es fr ja ms nb nn no pl pt pt_BR ru sk sv ta tr uk zh_CN"
AM_GLIB_GNU_GETTEXT


dnl ================================================================
dnl Start of pkg-config checks
dnl ================================================================
PKG_CHECK_MODULES(GLADE, gtk+-2.0 >= 2.6.0  libxml-2.0 >= 2.4.0)
AC_SUBST(GLADE_LIBS)
AC_SUBST(GLADE_CFLAGS)

AC_CHECK_LIB(popt, poptStrippedArgv,, AC_MSG_ERROR([popt 1.5 or newer is required to build
glade 3.0. You can download the latest version from ftp://people.redhat.com/sopwith/popt/]))


glade_version="${VERSION}"
glade_datadir='${datadir}/${PACKAGE}'
glade_libdir='${libdir}/${PACKAGE}'
glade_catalogsdir='${glade_datadir}/catalogs'
glade_modulesdir='${glade_libdir}/modules'
glade_pixmapsdir='${glade_datadir}/pixmaps'
glade_icondir='${datadir}/pixmaps'
glade_localedir='${prefix}/${DATADIRNAME}/locale'

AC_SUBST(glade_version)
AC_SUBST(glade_datadir)
AC_SUBST(glade_libdir)
AC_SUBST(glade_catalogsdir)
AC_SUBST(glade_modulesdir)
AC_SUBST(glade_pixmapsdir)
AC_SUBST(glade_icondir)
AC_SUBST(glade_localedir)


case $host_os in
*mingw* | pw32* | cygwin*)
  win32=yes
  AC_ARG_ENABLE(popt_dll,
                [  --disable-popt-dll         disable use of libpopt.dll (use static version instead)],
                popt_dll=$enableval, popt_dll=yes)
  if test "x$popt_dll" = "xyes"; then
    AC_DEFINE(USE_POPT_DLL, 1, [Define to 1 if libpopt.dll is to be used.])
  else
    AC_DEFINE(POPT_STATIC, 1, [Define to 1 if static version of libpopt is to be used.])
  fi
  ;;
esac
AM_CONDITIONAL(WITH_WIN32, test x"$win32" = "xyes")

AC_OUTPUT([
Makefile
pixmaps/Makefile
widgets/Makefile
po/Makefile.in
src/Makefile
src/libgladeui-1.0.pc
src/glade-paths.sh
])

echo "

Configuration:

	Source code location:	${srcdir}
	Compiler:		${CC} 

"
