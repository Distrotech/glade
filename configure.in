dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.52)
AC_INIT(src/glade-widget.c)
AC_CANONICAL_SYSTEM

AM_INIT_AUTOMAKE(glade, 2.90.0)
AM_MAINTAINER_MODE
AM_CONFIG_HEADER(config.h)

AC_PROG_INTLTOOL([0.29])
AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)
AC_PATH_PROG(DLLTOOL, dlltool)

AC_ISC_POSIX
AC_PROG_CC
AC_DISABLE_STATIC
AC_PROG_INSTALL
AC_PROG_MAKE_SET

AC_LIBTOOL_DLOPEN
AC_PROG_LIBTOOL


dnl ================================================================
dnl Gettext stuff.
dnl ================================================================
GETTEXT_PACKAGE=glade-3.0
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Name of the gettext package.])
ALL_LINGUAS="az es ms nn no pl pt ru sv ta tr uk"
AM_GLIB_GNU_GETTEXT


dnl ================================================================
dnl Start of pkg-config checks
dnl ================================================================
PKG_CHECK_MODULES(GLADE, gtk+-2.0 >= 2.4.0  libglade-2.0 >= 2.4.0  libxml-2.0 >= 2.4.0)
AC_SUBST(GLADE_LIBS)
AC_SUBST(GLADE_CFLAGS)

AC_CHECK_LIB(popt, poptStrippedArgv,, AC_MSG_ERROR([popt 1.5 or newer is required to build
glade 3.0. You can download the latest version from ftp://people.redhat.com/sopwith/popt/]))


dnl ================================================================
dnl GLADE_DATA_DIR - Sets the location of the data directory, where we install
dnl source files added to projects (such as all the Gnome m4 macros).
dnl This will substitute GLADE_DATA_DIR in config.h (from acconfig.h).
dnl ================================================================
if test "x${datadir}" = 'x${prefix}/share'; then
  if test "x${prefix}" = "xNONE"; then
    glade_data_dir="${ac_default_prefix}/share"
  else
    glade_data_dir="${prefix}/share"
  fi
else
  glade_data_dir="${datadir}"
fi
AC_DEFINE_UNQUOTED(GLADE_DATA_DIR, "${glade_data_dir}", [Data directory of Glade-3.])
AC_DEFINE_UNQUOTED(ICONS_DIR, "${glade_data_dir}/pixmaps", [Icons directory of Glade-3.])
AC_DEFINE_UNQUOTED(LOCALE_DIR, "${glade_data_dir}/locale", [Locale directory of Glade-3.])

dnl ================================================================
dnl PIXMAPS, WIDGETS, CATALOGS
dnl ================================================================
if test "x${prefix}" = "xNONE"; then
  tmp="${ac_default_prefix}"
else
  tmp="${prefix}"
fi
pixmapsdir="${tmp}/${DATADIRNAME}/${PACKAGE}-${VERSION}/pixmaps"
widgetsdir="${tmp}/${DATADIRNAME}/${PACKAGE}-${VERSION}/widgets"
catalogsdir="${tmp}/${DATADIRNAME}/${PACKAGE}-${VERSION}/catalogs"
AC_DEFINE_UNQUOTED(PIXMAPS_DIR, "$pixmapsdir", [Pixmaps directory of Glade-3.])
AC_DEFINE_UNQUOTED(WIDGETS_DIR, "$widgetsdir", [Widgets directory of Glade-3.])
AC_DEFINE_UNQUOTED(CATALOGS_DIR, "$catalogsdir", [Catalogs directory of Glade-3.])

dnl ================================================================
dnl MODULES
dnl ================================================================
if test "x${libdir}" = "xNONE"; then
  modulesdir="${ac_default_libdir}/${PACKAGE}"
else
  modulesdir="${libdir}/${PACKAGE}"
fi

AC_SUBST(pixmapsdir)
AC_SUBST(widgetsdir)
AC_SUBST(catalogsdir)
AC_SUBST(modulesdir)

case $host_os in
*mingw* | pw32* | cygwin*)
  win32=yes
  AC_ARG_ENABLE(popt_dll,
                [  --disable-popt-dll         disable use of libpopt.dll (use static version instead)],
                popt_dll=$enableval, popt_dll=yes)
  if test "x$popt_dll" = "xyes"; then
    AC_DEFINE(USE_POPT_DLL, 1, [Define to 1 if libpopt.dll is to be used.])
  else
    AC_DEFINE(POPT_STATIC, 1, [Define to 1 if static version of libpopt is to be used.])
  fi
  ;;
esac
AM_CONDITIONAL(WITH_WIN32, test x"$win32" = "xyes")

AC_OUTPUT([
Makefile
pixmaps/Makefile
widgets/Makefile
po/Makefile.in
src/Makefile
src/libgladeui-1.0.pc
])

echo "

Configuration:

	Source code location:	${srcdir}
	Compiler:		${CC} 

"
